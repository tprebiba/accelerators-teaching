/******************************************************************************************
 * BOOSTER
 ******************************************************************************************/
 call, file = 'psb.seq';
 call, file = 'psb_fb_lhc.str';
 call, file = 'psb_aperture.dbx';

/******************************************************************************************
 * beam, use
 ******************************************************************************************/

beam, particle=PROTON, pc=0.5708301551893517;

use, sequence=psb1;


/******************************************************************************************
 * Match for desired working point
 ******************************************************************************************/

!QH = 4.17; ! given as globals when defining the cpymad.Madx instance
!QV = 4.23; ! given as globals when defining the cpymad.Madx instance

MATCH, Sequence=psb1;
	VARY, NAME = kbrqf, STEP = 1e-3;
	VARY, NAME = kbrqd, STEP = 1e-3;
	GLOBAL, Q1 = QH;
	GLOBAL, Q2 = QV;
	JACOBIAN,CALLS=1000,TOLERANCE=1.0E-18,STRATEGY=3;
ENDMATCH; 

select, flag=twiss, clear;
select, flag=twiss, column=name, s,betx,bety,dx;
twiss, sequence=psb1;!, file=psb/twiss_thick;

/******************************************************************************************
 * Make thin sequence for use in Xsuite
 ******************************************************************************************/
seqedit, sequence = psb1;
flatten;
refer=centre;
endedit;
use, sequence=psb1;

if (thin>0){
	select, flag = MAKETHIN, clear;
	select, flag=MAKETHIN, SLICE=slices, thick=false;
	!MAKETHIN, SEQUENCE=psb1, STYLE=SIMPLE;
	MAKETHIN, SEQUENCE=psb1, STYLE=TEAPOT;
};

/******************************************************************************************
 * Install markers for SC kicks
 * NOT needed ?
 ******************************************************************************************/
/*
seqedit, sequence = psb1;
flatten;
markers : marker;
t=0;
circumference=table(twiss,psb1$END,s);
nkicks=160;
while (t<nkicks){
	loc=circumference/nkicks*t;
	install, element = markers,  at=loc;
	t=t+1;
	};
flatten;
endedit;
use, sequence=psb1;
*/

/******************************************************************************************
 * Install single RF cavity
 ******************************************************************************************/
CAVFINEM	: RFCAVITY, L:= 1e-6, VOLT:= 0.008*0, HARMON = 1, LAG:= 0.0, no_cavity_totalpath;
select, flag = seqedit, clear;
!select, flag = seqedit, class=BR_ACWFB;
seqedit, sequence = psb1;
flatten;
!remove, element = selected;
install, element = BR.C02, class=CAVFINEM, at=-1.8-1.5, from=BR.BHZ51;
flatten;
endedit;

use, sequence=psb1;

MATCH, Sequence=psb1;
	VARY, NAME = kbrqf, STEP = 1e-3;
	VARY, NAME = kbrqd, STEP = 1e-3;
	GLOBAL, Q1 = QH;
	GLOBAL, Q2 = QV;
	JACOBIAN,CALLS=1000,TOLERANCE=1.0E-18,STRATEGY=3; !,COOL=real,BALANCE=real, random=real;
ENDMATCH; 

select, flag=twiss, clear;
select, flag=twiss, column=name, s,betx,bety,dx;
twiss, sequence=psb1;!, file=twiss_thin;
